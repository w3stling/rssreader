plugins {
    id 'org.sonarqube' version '2.8'
    id 'com.jfrog.bintray' version '1.8.5'
    id 'com.jfrog.artifactory' version '4.15.2'
    id 'org.ajoberstar.git-publish' version '2.1.3'
    id 'net.researchgate.release' version '2.8.1'
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'jacoco'
apply plugin: 'distribution'

group 'com.apptasticsoftware'
version "${version}"

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
    jcenter()

    maven {
        url  "https://jcenter.bintray.com"
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'
    testCompile group: 'com.github.npathai', name: 'hamcrest-optional', version: '2.0.0'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '3.2.0'
}

distTar {
    compression = Compression.GZIP
    extension = 'tar.gz'
}

distributions {
    main {
        contents {
            from("$projectDir")
            exclude('.gitignore')
            exclude('.travis.yml')
            exclude('*.iml')
            exclude('out')
            exclude('build')
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

ext.moduleName = 'com.apptastic.rssreader'

compileJava {
    options.encoding = "UTF-8"
    inputs.property('moduleName', moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath
        ]
        classpath = files()
    }
}

compileTestJava {
    options.encoding = "UTF-8"
}

jacoco {
    toolVersion = "0.8.5"
}

jacocoTestReport {
    reports {
        xml.enabled true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
    }
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

bintray {
    if (!project.version.endsWith("-SNAPSHOT")) {
        user = System.getenv('BINTRAY_USER')
        key = System.getenv('BINTRAY_KEY')
        publications = ['RssReaderPublication']
        dryRun = false
        publish = true
        override = true

        pkg {
            repo = 'maven-repo'
            name = 'rssreader'
            desc = 'Java library for extracting data from the Financial Supervisory Authority (Finansinspektionen) short selling registry.'
            issueTrackerUrl = 'https://github.com/w3stling/rssreader/issues'
            vcsUrl = 'https://github.com/w3stling/rssreader.git'
            licenses = ['MIT']

            version {
                name = System.properties['version']
                released = new Date()
            }
        }
    }
}

artifactory {
    contextUrl = 'https://oss.jfrog.org/artifactory'
    publish {
        repository {
            if (project.version.endsWith("-SNAPSHOT")) {
                repoKey = 'oss-snapshot-local'
            } else {
                repoKey = 'oss-release-local'
            }
            username = System.getenv('ARTIFACTORY_USERNAME')
            password = System.getenv('ARTIFACTORY_PASSWORD')
            maven = true
        }
        defaults {
            publications ('RssReaderPublication')
        }
    }
}

publishing {
    publications {
        RssReaderPublication(MavenPublication) {
            from components.java
            groupId 'com.apptastic'
            artifactId 'rssreader'
            version "${version}"

            artifact sourcesJar
            artifact javadocJar
        }
    }
}

gitPublish {
    repoUri = 'https://github.com/w3stling/rssreader.git'
    branch = 'gh-pages'

    contents {
        from("$buildDir/docs/javadoc") {
            into "javadoc/${version}"
        }
    }

    preserve {
        include 'javadoc/**'
        exclude 'javadoc/*-SNAPSHOT/**'
    }

    commitMessage = 'Publishing javadoc'
}

release {
    tagTemplate = 'v$version'
}
